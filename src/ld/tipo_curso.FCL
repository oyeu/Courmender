FUNCTION_BLOCK determinar_tipo	// Block definition (there may be more than one block per file)

// Define input variables
VAR_INPUT				
	tiempo_disponible: REAL;
	tiempo_esperado: REAL;
END_VAR

// Define output variable
VAR_OUTPUT				
	tipo_curso : REAL;
END_VAR

// Fuzzify input variable 'tiempo_disponible': {'poco', 'medio' , 'alto'}
FUZZIFY tiempo_disponible			
	TERM poco := (0.1, 1) (3, 0) ; 
	TERM medio := (2, 0) (4, 1) (5, 1) (6,0);
	TERM alto := (5, 0) (9, 1);
END_FUZZIFY

// Fuzzify input variable 'tiempo_esperado': {'poco', 'medio' , 'mucho'}
FUZZIFY tiempo_esperado		
	TERM poco := (0, 1) (1, 1) (2,0) ; 
	TERM medio := (1, 0) (2, 1) (5,0);
	TERM mucho := (3.5, 0) (6, 1);
END_FUZZIFY

// Defuzzify output variable 'tipo_curso' : {'intensivo', 'diplomado', 'bimestral', 'trimestral', 'semestral', 'anual'}
DEFUZZIFY tipo_curso	
	TERM taller := (0, 1) (0.25, 0);	
	TERM intensivo := (0, 0) (0.25, 1) (0.5, 0);
	TERM diplomado := (0.25, 0) (0.5, 1) (0.75, 0);
	TERM bimestral := (0.5, 0) (0.75, 1) (1,0);
	TERM trimestral := (0.75, 0) (1, 1) (1.25,0);
	TERM semestral := (1,0) (1.25, 1) (1.50,0);
	TERM anual := (1.25,0) (1.50,1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

	RULE 1: IF tiempo_disponible IS poco AND tiempo_esperado IS poco THEN tipo_curso IS taller;
	RULE 2: IF tiempo_disponible IS poco AND tiempo_esperado IS medio THEN tipo_curso IS bimestral;
	RULE 3: IF tiempo_disponible IS poco AND tiempo_esperado IS mucho THEN tipo_curso IS anual;
	RULE 4: IF tiempo_disponible IS medio AND tiempo_esperado IS poco THEN tipo_curso IS diplomado;
	RULE 5: IF tiempo_disponible IS medio AND tiempo_esperado IS medio THEN tipo_curso IS trimestral;
	RULE 6: IF tiempo_disponible IS medio AND tiempo_esperado IS mucho THEN tipo_curso IS semestral;
	RULE 7: IF tiempo_disponible IS alto AND tiempo_esperado IS poco THEN tipo_curso IS intensivo;
	RULE 8: IF tiempo_disponible IS alto AND tiempo_esperado IS medio THEN tipo_curso IS diplomado;
	RULE 9: IF tiempo_disponible IS alto AND tiempo_esperado IS mucho THEN tipo_curso IS bimestral;
	
END_RULEBLOCK

END_FUNCTION_BLOCK